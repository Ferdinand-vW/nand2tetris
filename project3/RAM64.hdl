// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/RAM64.hdl
/**
 * Memory of sixty four 16-bit registers.
 * If load is asserted, the value of the register selected by
 * address is set to in; Otherwise, the value does not change.
 * The value of the selected register is emitted by out.
 */
CHIP RAM64 {
    IN in[16], load, address[6];
    OUT out[16];

    PARTS:
    DMux8Way(in=load, sel=address[3..5], a=ram0Load, b=ram1Load, c=ram2Load,
             d=ram3Load, e=ram4Load, f=ram5Load, g=ram6Load, h=ram7Load );
    RAM8(in=in, load=ram0Load, address=address[0..2], out=ram0);
    RAM8(in=in, load=ram1Load, address=address[0..2], out=ram1);
    RAM8(in=in, load=ram2Load, address=address[0..2], out=ram2);
    RAM8(in=in, load=ram3Load, address=address[0..2], out=ram3);
    RAM8(in=in, load=ram4Load, address=address[0..2], out=ram4);
    RAM8(in=in, load=ram5Load, address=address[0..2], out=ram5);
    RAM8(in=in, load=ram6Load, address=address[0..2], out=ram6);
    RAM8(in=in, load=ram7Load, address=address[0..2], out=ram7);
    Mux8Way16(a=ram0, b=ram1, c=ram2, d=ram3, e=ram4, f=ram5, g=ram6, h=ram7, sel=address[3..5], out=out);
}